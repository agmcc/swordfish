import org.gradle.internal.jvm.Jvm

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'jacoco'
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

group 'com.github.agmcc'

version = '0.2.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

testSets {
    integrationTest
}

dependencies {
    /* PRODUCTION */
    api 'javax.inject:javax.inject:1'

    implementation 'com.squareup:javapoet:1.11.1'
    implementation 'com.google.guava:guava:28.0-jre'

    /* INTEGRATION TEST */
    integrationTestImplementation 'com.google.truth:truth:0.30'
    integrationTestImplementation 'com.google.testing.compile:compile-testing:0.18'

    // Needed for JDK 8
    if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
        integrationTestImplementation files(Jvm.current().getToolsJar())
    }
}

tasks.withType(Test) {
    testLogging {
        events 'failed', 'standard_error'
        exceptionFormat = 'full'
    }
}

googleJavaFormat {
    toolVersion = '1.7'
}

task format {
    dependsOn ':googleJavaFormat'
    group 'build'
    description 'Google Java Format'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'swordfish'
        }
    }
}

jacocoTestReport {
    dependsOn check
    reports {
        xml.enabled true
        html.enabled false
    }
    executionData = fileTree(buildDir).include("/jacoco/*.exec")
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
