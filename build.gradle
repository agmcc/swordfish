import org.gradle.internal.jvm.Jvm

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'jacoco'
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

group 'com.github.agmcc'

version = '0.2.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
    junitVersion = '5.4.2'
    mockitoVersion = '2.28.2'
}

repositories {
    mavenCentral()
}

testSets {
    integrationTest
}

configurations {
    integrationTestImplementation {
        exclude group: 'org.junit.jupiter'
    }
}

dependencies {
    /* PRODUCTION */
    api 'javax.inject:javax.inject:1'

    implementation 'com.squareup:javapoet:1.11.1'
    implementation 'com.google.guava:guava:28.0-jre'

    /* UNIT TEST */
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    testImplementation 'org.assertj:assertj-core:3.13.2'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    /* INTEGRATION TEST */
    integrationTestImplementation 'com.google.truth:truth:0.30'
    integrationTestImplementation 'com.google.testing.compile:compile-testing:0.18'

    integrationTestRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junitVersion"

    // Needed for JDK 8
    if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
        integrationTestImplementation files(Jvm.current().getToolsJar())
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events 'failed', 'standard_error'
        exceptionFormat = 'full'
    }
}

googleJavaFormat {
    toolVersion = '1.7'
}

task format {
    dependsOn ':googleJavaFormat'
    group 'build'
    description 'Google Java Format'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'swordfish'
        }
    }
}

jacocoTestReport {
    dependsOn check
    reports {
        xml.enabled true
    }
    executionData = fileTree(buildDir).include("/jacoco/*.exec")
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

// Needed to suppress "internal proprietary API" warnings
compileTestJava {
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'
}